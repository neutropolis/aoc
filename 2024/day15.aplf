(p1 p2)←day15 input
 i←(×∘≢¨⊆⊢)⊃⎕NGET input 1
 z←(↑⊃)i ⋄ m←⊃,/(⊃⌽)i
 G←{
     ~×≢⍵:⍺
     m←⊃⍵
     v←(¯1 0)(0 1)(1 0)(0 ¯1)⊃⍨'^>v<'⍳m
     a←⍺
     s←⊃⍸'@'=⍺
     F←{⍺←1⋄'.#'∊⍨a⊃⍨⊂⍵+⍺×v:⍺⋄⍵∇⍨⍺+1}
     p←(⊂s)+(⊂v)×0,⍳F s
     '.'=a⌷⍨⊃⌽p:((¯1⌽a[p])@p⊢a)∇1↓⍵
     a∇1↓⍵
 }
 p1←+/∊(⊂100 1)×¯1+⍸'O'=z G m

 z←↑,/((,¨'#O@.')⎕R'##' '[]' '@.' '..')¨z
 H←{
     ~×≢⍵:⍺
     m←⊃⍵
     v←(¯1 0)(0 1)(1 0)(0 ¯1)⌷⍨'^>v<'⍳m
     a←⍺
     s←⊃⍸'@'=⍺
     F←{
         ∨/'#'∊⍨a[n←(⊃⍵)+v]:a
         ~∧/'.'∊⍨a[n]:∇⍵,⍨⊂(×≢¨foo)/foo←∪⊃,/{'@'=⍵⌷a:⊂⍵⋄'['=⍵⌷a:⍵(⍵+0 1)⋄']'=⍵⌷a:(⍵+0 ¯1)⍵⋄⊂⍬}¨n
         Upd←{c←a[(⊂⍵)+v]⋄a[(⊂⍵)+v]←a[⊂⍵]⋄a[⊂⍵]←c}¨⍵
     }
     m∊'<>':(a G 1↑m)∇1↓⍵
     F 1⍴⊂⊂s ⋄ a∇1↓⍵
 }
 p2←+/∊(⊂100 1)×¯1+⍸'['=z H m
