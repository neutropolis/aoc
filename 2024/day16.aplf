(p1 p2)←day16 input
 i←↑⊃⎕NGET input 1
 mx←2147483647 ⍝ just a big number
 d←'∨><∧'
 E←⊃⍸i='E'
 z←{⍵='S':⊂0(d⍳'>') ⋄ ⍵∊'.E':⊂mx 5 ⋄ ⍵}¨i
 Score←{
     (⊃⍵)≡'#':1+mx
     mx=⊃⊃⍵:1+mx
     (⊃⊃⍵)+1+0 1000 1000 1000[d[⊃⌽⊃⍵]⍳⍨d⌽⍨¯1+d⍳⍺]
 }
 Step←{
     c←⊃(⊂2 2)⊃⍵
     c≡'#':⊂c
     s←(⊂c),d(Score,(d∘⍳⊣))¨(9⍴0 1)/,⍵
     ⊂s[((⊃⍸)⊢=⌊/)⊃¨s]
 }
 p1←⊃∊E⌷out←(Step⌺3 3⍣≡)z
 ⍝ {⍵≡'#':⍵ ⋄ (d,'.')[⊃⌽⊃⍵]}¨out

 (E⌷out)←⊂⊂(⊃∊E⌷out)0
 Step←{
     c←(⊂2 2)⊃⍵
     c≡'#':⊂c
     0=⊃⌽⊃c:⊂c
     ngh←(9⍴0 1)/,⍵
     F←{⍵='>':3⋄⍵='<':2⋄⍵='∧':1⋄⍵='∨':4⋄5}
     pnt←ngh⊃⍨F d[⊃⌽⊃c]
     (∨/{0=⊃⌽⊃⍵}¨x←ngh[1 4])∧(2 3∊⍨⊃⌽⊃c)∧0≠⊃⌽⊃pnt:⊂⊂(1000+⊃⊃c)0 ⍝ Need to check descending values +1000
     (∨/{0=⊃⌽⊃⍵}¨x←ngh[2 3])∧(1 4∊⍨⊃⌽⊃c)∧0≠⊃⌽⊃pnt:⊂⊂(1000+⊃⊃c)0
     0≠⊃⌽⊃pnt:⊂c
     ((⊃⊃pnt)>⊃⊃c)∧0=⊃⌽⊃pnt:⊂⊂(⊃⊃c)0
     ⊂c
 }
 out2←(Step⌺3 3⍣≡)out
 p2←+/+/0={⊃⌽⊃⍵}¨out2

 ⍝ {⍵≡'#':⍵ ⋄ (d,'O')[⊃⌽⊃⍵]}¨out2
