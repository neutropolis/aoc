 (p1 p2)←day21 input
 in←⊃⎕NGET input 1
 nk←4 3⍴'789456123 0A' ⋄ nz←4 3
 dk←2 3⍴' ∧A<∨>' ⋄ dz←1 3
 n←{(0⌈⍵)⍴'∧'} ⋄ s←{(|0⌊⍵)⍴'∨'}
 w←{(0⌈⍵)⍴'<'} ⋄ e←{(|0⌊⍵)⍴'>'}
 T←{
     ~×≢⍵:''
     (k p)←⍺
     (i j)←p-t←⊃⍸k=⊃⍵
     path←(w j),(s i),(n i),e j
     ((1=⊃⌽t)∧4=⊃p)∨(1=⊃⌽p)∧4=⊃t:(⌽path),'A',(k t)∇ 1↓⍵
     path,'A',(k t)∇ 1↓⍵
 }
 T2←{
     ~×≢⍵:''
     (k p)←⍺
     (i j)←p-t←⊃⍸k=⊃⍵
     path←(w j),(s i),(n i),e j
     ((1=⊃⌽t)∧1=⊃p)∨(1=⊃⌽p)∧1=⊃t:(⌽path),'A',(k t)∇ 1↓⍵
     path,'A',(k t)∇ 1↓⍵
 }
 nr←(nk nz)∘T
 dr←(dk dz)∘T2
 p1←((⍎3∘↑)¨in)+.×(≢∘dr∘dr∘nr)¨in

 m←{(f t)←⍵ ⋄ dk(⊃⍸f=dk)T2,t}¨cs←∘.,⍨'A∧∨<>'
 l24←({L←⍵⋄{1=≢⍵:1⋄+/∊{L[⍸⍵∘≡¨cs]}¨2,/'A',⍵}¨m}⍣24)l0
 p2←((⍎3∘↑)¨in)+.×{+/∊{l24[⍸⍵∘≡¨cs]}¨2,/'A',(nk nz) T ⍵}¨in
